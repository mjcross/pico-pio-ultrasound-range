;--------------------------------------
; Generate 41.118 kHz transmit carrier
; with sync pulse on falling edge
;--------------------------------------

.program tx
.side_set 1                 ; assign one side-set bit

PUBLIC entry:
.wrap_target

;; GPIO high for 16 + 32 x (15+16+16) = 1,520 clock ticks
    set x, 31 [15]           side 1
high_loop:
    nop [14]                side 1
    nop [15]                side 1
    jmp x-- high_loop [15]  side 1

;; GPIO low for (1+1+14) + 32 x (15+16+16) = 1,520 clock ticks
    irq set 7               side 0      ; sync pulse (1 tick)
    irq clear 7             side 0
    set x, 31               side 0
low_loop:
    nop [14]                side 0
    nop [15]                side 0
    jmp x-- low_loop [15]   side 0
.wrap

; 2 instructions (2 clocks per cycle)


% c-sdk {
static inline void tx_sm_init (PIO pio, uint sm, uint offset, uint gpio) {
    // create and initialise a state machine configuration
    pio_sm_config config = tx_program_get_default_config (offset);

    // update the configuration with the base sideset pin
    sm_config_set_sideset_pins (&config, gpio);

    // apply the configuration to the state machine and initialise program counter to 'entry'
    pio_sm_init (pio, sm, offset + tx_offset_entry, &config);

    // allow the state machine to run
    pio_sm_set_enabled (pio, sm, true);
}
%}
