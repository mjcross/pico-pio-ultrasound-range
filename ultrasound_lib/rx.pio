.program rx

;;---------------------------------------------------
;; Measure delay of received falling edge, accumulating
;; count over 16 carrier cycles
;;---------------------------------------------------

.wrap_target
;; send result
    mov isr, !x
    push noblock

;; reset counters
PUBLIC entry:
    mov x, !null        ; set phase counter to -1
    set y, 15           ; accumulate count over 16 carrier cycles

;; synchronise to TX falling edge
cycle_loop:
    wait 1 irq 7

;; count while RX is low
loop_low:
    jmp pin loop_high
    jmp x-- loop_low

;; count while RX is high
loop_high:
    jmp x-- over
over:
    jmp pin loop_high

;; accumulate count
    jmp y-- cycle_loop
.wrap

;; 10 instructions, 2 ticks per count (= 16ns, 0.0055mm)

% c-sdk {
static inline void rx_sm_init (PIO pio, uint sm, uint offset, uint gpio) {
    // create and initialise a state machine configuration
    pio_sm_config config = rx_program_get_default_config (offset);

    // define the raw GPIO to be used for JMP PIN
    sm_config_set_jmp_pin(&config, gpio);

    // join the FIFOs for receive
    sm_config_set_fifo_join(&config, PIO_FIFO_JOIN_RX);

    // apply the configuration to the state machine and initialise program counter to 'entry'
    pio_sm_init (pio, sm, offset + rx_offset_entry, &config);

    // allow the state machine to run
    pio_sm_set_enabled (pio, sm, true);
}
%}